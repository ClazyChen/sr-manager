# 进入战场之后的交互界面
include("血条.jl")

function 打印单位(单位::角色)
    print(单位)
    if 单位.存活
        println(" $(绘制血条(单位.生命.当前)) $(单位.生命)")
    else
        println(@红 " 已阵亡")
    end
    print("  韧性 $(@灰 绘制进度条(单位.韧性))")
    print("  能量 $(@黄 绘制进度条(单位.能量))")
    println()
end

function 打印战场信息(战场::战场)
    if 当前界面控制.开启打印战场信息
        println(@青 "蓝方战技点 $(绘制进度条(战场.蓝方战技点, 已完成符号 = "★ ", 未完成符号 = "☆ "))")
        foreach(打印单位, 战场.蓝方角色)
        println(@红 "红方战技点 $(绘制进度条(战场.红方战技点, 已完成符号 = "★ ", 未完成符号 = "☆ "))")
        foreach(打印单位, 战场.红方角色)
    end
end

function 打印速度条(战场::战场)
    if 当前界面控制.开启打印速度条
        println("【$(当前角色(战场.速度条))】的回合")
        for i in 2:length(战场.速度条.节点)
            print("$(战场.速度条.节点[i].单位)【$(战场.速度条.节点[i].下次行动的时间 - 战场.速度条.全局时间)】")
        end
        println()
    end
end

macro 进行流程(战场::Symbol, 流程::Expr)
    quote
        $(esc(流程))
        已经结束($(esc(战场))) && break
    end
end

function 执行游戏流程!(战场::战场)::标签.队伍
    初始化技能!(战场)
    初始化速度条!(战场)
    while !已经结束(战场)
        控制台换行()
        时间推进!(战场.速度条)
        打印速度条(战场)
        打印战场信息(战场)
        等待用户输入()
        @进行流程 战场 begin
            进行回合!(战场)
        end
        更新!(战场.速度条)
    end
    战场.胜利方
end