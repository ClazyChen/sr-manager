# 进入战场之后的交互界面
include("血条.jl")

function 打印单位(单位::角色)
    print(单位)
    if 单位.存活
        单位盾值 = 盾值(单位)
        盾条 = 单位盾值 > 0 ? " $(绘制盾条(单位盾值))" : ""
        盾值说明 = 单位盾值 > 0 ? " + $(单位盾值 / 10)" : ""
        println(" $(绘制血条(单位.生命.当前))$盾条 $(单位.生命)$盾值说明")
    else
        println(@红 " 已阵亡")
    end
    print("  韧性 $(@灰 绘制进度条(单位.韧性))")
    print("  能量 $(@黄 绘制进度条(单位.能量))")
    if 单位.技力.上限 > 0
        print("  技力 $(@深青 绘制进度条(单位.技力))")
    end
    for 状态 in 单位.状态列表
        print("【$状态$(状态.持续时间)】")
    end
    println()
end

function 打印战场信息(战场::战场)
    if 当前界面控制.开启打印战场信息
        println(@青 "蓝方战技点 $(绘制进度条(战场.蓝方战技点, 已完成符号 = "★ ", 未完成符号 = "☆ "))")
        foreach(打印单位, 战场.蓝方角色)
        println(@红 "红方战技点 $(绘制进度条(战场.红方战技点, 已完成符号 = "★ ", 未完成符号 = "☆ "))")
        foreach(打印单位, 战场.红方角色)
    end
end

function 打印速度条(战场::战场)
    if 当前界面控制.开启打印速度条
        println("【$(当前角色(战场.速度条))】的回合")
        for i in 2:length(战场.速度条.节点)
            显示速度 = 战场.速度条.节点[i].下次行动的时间 - 战场.速度条.全局时间
            显示速度 = round(显示速度, digits = 1)
            print("$(战场.速度条.节点[i].单位)【$显示速度】")
        end
        println()
    end
end

