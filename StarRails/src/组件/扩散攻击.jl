# 扩散攻击的模板

# 选择扩散攻击的目标，返回目标和预期伤害
function 选择扩散攻击的目标(战场::战场, 来源::角色, 主目标威力::Int, 副目标威力::Int)
    期望函数 = (目标::角色) -> begin
        预期伤害 = 攻击预期伤害(来源, 目标, 主目标威力)
        左侧副目标 = 左侧角色(战场, 目标)
        右侧副目标 = 右侧角色(战场, 目标)
        if 左侧副目标 !== nothing
            预期伤害 += 攻击预期伤害(来源, 左侧副目标, 副目标威力)
        end
        if 右侧副目标 !== nothing
            预期伤害 += 攻击预期伤害(来源, 右侧副目标, 副目标威力)
        end
        预期伤害
    end
    return 根据期望函数选择目标(战场, 来源, 期望函数)
end

function 扩散选定(战场::战场, 主目标::角色)
    选定目标::Vector{角色} = [主目标]
    左侧副目标 = 左侧角色(战场, 主目标)
    右侧副目标 = 右侧角色(战场, 主目标)
    if 左侧副目标 !== nothing
        push!(选定目标, 左侧副目标)
    end
    if 右侧副目标 !== nothing
        push!(选定目标, 右侧副目标)
    end
    return 选定目标
end

macro 扩散攻击()
    quote
        @目标 扩散攻击
        @效果 主目标(技能) begin
            攻击并削韧!(事件, 目标 = 目标, 威力 = 技能.主目标威力, 削韧 = 技能.主目标削韧)
        end
        @效果 副目标(技能) begin
            攻击并削韧!(事件, 目标 = 目标, 威力 = 技能.副目标威力, 削韧 = 技能.副目标削韧)
        end
    end
end