# 损失生命
# 受到伤害或者我方技能消耗生命都算在内

@创建事件 损失生命 begin

    数值::Int
    下降前的生命值::Int
    下降后的生命值::Int

    function 损失生命(战场::战场; 来源::角色, 目标::角色, 数值::Int, 性质::Vector{标签.性质}=[])
        new(战场, 性质, 来源, 目标, 数值, 目标.生命, 目标.生命 - 数值)
    end

end

@触发事件 begin
    事件.目标.生命 -= 事件.数值
    控制台换行("$事件")
    if 事件.目标.生命.当前 <= 0
        造成致命伤害!(事件)
    end
end

Base.show(io::IO, 事件::损失生命) = print(io, "【$(事件.目标)】生命值下降 $(事件.数值 / 10.0) 点，剩余生命值 $(事件.目标.生命.当前 / 10.0) 点。")

function 扣减生命!(事件::事件; 数值::Int)
    触发(损失生命(事件.战场, 来源 = 事件.来源, 目标 = 事件.目标, 数值 = 数值, 性质 = 事件.性质列表))
end

function 消耗生命!(事件::事件; 数值::Int)
    触发(损失生命(事件.战场, 来源 = 事件.来源, 目标 = 事件.来源, 数值 = min(数值, 事件.来源.生命.当前 - 1), 性质 = 事件.性质列表))
end

扣减生命!(事件::事件; 数值::AbstractFloat) = 扣减生命!(事件, 数值 = round(Int, 数值 * 10))
消耗生命!(事件::事件; 数值::AbstractFloat) = 消耗生命!(事件, 数值 = round(Int, 数值 * 10))
