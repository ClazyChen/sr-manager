# 发动攻击，核心机制

@创建事件 发动攻击 begin
    
    威力::Int
    属性::标签.属性

    来源攻击::Int
    来源暴击::Int
    来源穿甲::Int
    目标防御::Int

    造成伤害::Int
    造成暴击::Bool
    造成穿甲::Bool

    function 发动攻击(战场::战场, 指向, 威力::Int; 性质::Array{标签.性质}=[])
        local 来源 = 指向.first
        local 目标 = 指向.second
        local 属性 = 来源.模板.属性
        new(战场, 性质, 来源, 目标, 威力, 属性, 来源.攻击, 来源.暴击, 来源.穿甲, 目标.防御, 0, false, false)
    end

end

@触发事件 begin
    
    事件.造成暴击 = 随机判定(事件.来源暴击)
    事件.造成穿甲 = 随机判定(事件.来源穿甲)
    事件.造成伤害 = 事件.来源攻击 * 事件.威力 ÷ 10
    if !事件.造成穿甲 || 事件.目标防御 < 0
        事件.造成伤害 -= 事件.目标防御 * 10
    end
    if 事件.造成暴击
        事件.造成伤害 = 事件.造成伤害 * 3 ÷ 2
    end
    事件.造成伤害 = max(事件.造成伤害, 1)

    控制台换行("$事件")

    if 事件.造成伤害 > 0
        @扣减生命 begin
            数值 = 事件.造成伤害
        end
    end
end

function Base.show(io::IO, 事件::发动攻击)
    local 暴击穿甲说明 = ""
    if 事件.造成暴击 && 事件.造成穿甲
        暴击穿甲说明 = @绿 "暴击且穿甲！"
    elseif 事件.造成暴击
        暴击穿甲说明 = @绿 "暴击！"
    elseif 事件.造成穿甲
        暴击穿甲说明 = @绿 "穿甲！"
    end
    print(io, "【$(事件.来源)】对【$(事件.目标)】发动攻击，造成了 $(事件.造成伤害 / 10.0) 点 $(事件.属性) 伤害。$暴击穿甲说明")
end

macro 攻击目标(说明::Expr)
    威力表达式 = 提取字段(:威力, 说明)
    目标表达式 = 提取字段(:目标, 说明, 默认值 = :(事件.目标))
    quote
        $(esc(:触发))(发动攻击(
            $(esc(:事件)).战场,
            $(esc(:事件)).来源 => $(esc(目标表达式)),
            $(esc(威力表达式)), 性质 = $(esc(:事件)).性质列表))
    end
end